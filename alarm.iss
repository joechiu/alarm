; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Clock Alarm"
#define MyAppVersion "2018"
#define MyAppPublisher "Joe Chiu"
#define MyAppURL "http://eland.nz/"
#define MyAppExeName "alarm.exe"
#define SourcePath "D:\dev\python\exe\alarm"
#define BuildPath "build\exe.win-amd64-3.6"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{352BF9EC-67B9-447E-A64F-4C41400B9B49}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\Clock Alarm
DisableProgramGroupPage=yes
LicenseFile={#SourcePath}\alarm.rtf
OutputDir={#SourcePath}
OutputBaseFilename=alarm
SetupIconFile={#SourcePath}\icon.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Code]
var
  dbChecked: Boolean;
  dbCheckResult: Boolean;

function InitDB : Boolean;
begin
  if not dbChecked then begin
    Log('InitDB called...............');
    dbCheckResult := MsgBox('Initialize Database:' #13#13 'Do you want to initialize the data? The data will lost if clicked Yes.', mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = idYes;
    dbChecked := True;
  end;
  Result := dbCheckResult;
end;

[Dirs]
Name: {app}; Permissions: users-full
Name: "{app}\db"; Permissions: users-full

[Files]
Source: "{#SourcePath}\{#BuildPath}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\{#BuildPath}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {app}\db\*; DestDir: {app}\db\backup-{code:GetTodaysName}; Flags: external setntfscompression skipifsourcedoesntexist; Check: InitDB
Source: "{#SourcePath}\init\*.db"; DestDir: "{app}\db\"; Flags: ignoreversion recursesubdirs createallsubdirs; Permissions: users-full; Check: InitDB
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
var
  TodaysName : String;

function GetToday : String;
begin
  Result := GetDateTimeString ('yyyymmdd-hh.nn.ss', '-', #0);
end;

function GetTodaysName (Param: String): String;
begin
  if ('' = TodaysName) then
  begin
    TodaysName := GetToday ();
  end;
  Result := TodaysName;
end;

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

